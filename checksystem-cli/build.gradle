plugins {
    id 'java'
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'ru.clevertec.checksystem.plugin.pdfprint' version "1.3.0"
}

group = 'ru.clevertec.checksystem'
version = '1.0'
sourceCompatibility = '14'

def coreProject = project(":checksystem-core")
def coreMainResourcesDir = "$coreProject.projectDir/src/main/resources"
def applicationMainClass = 'ru.clevertec.checksystem.cli.ConsoleApplication'

bootJar {
    mainClass.set(applicationMainClass)
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources {
            srcDir coreMainResourcesDir
        }
    }
}

configurations.all {
    exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
}

dependencies {
    implementation coreProject

    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

def applyJvmArgs = { o ->
    o.jvmArgs += ["--add-opens", "java.base/java.lang=ALL-UNNAMED"]
    coreProject.configurations.runtimeAgent.each { o.jvmArgs "-javaagent:$it.absolutePath" }
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    doFirst { t -> applyJvmArgs(t) }
}

tasks.withType(JavaExec) {
    it.each { t -> doFirst { applyJvmArgs(t) } }
}

def hasPdfTemplate = true

pdfPrint {
    showInfo = true

    inputPath = "$rootDir\\..\\examples\\serialized.json"
    inputFormat = "json"
    outputPath = "$rootDir\\resources\\checks.pdf"

    hasTemplate = hasPdfTemplate
    templateUrl = "https://github.com/stebadmitriy/files/raw/main/Clevertec_Template.pdf"
    templateTopOffset = 94
    templateOutputPath = "$rootDir\\resources\\templates\\Clevertec_Template.pdf"
}
