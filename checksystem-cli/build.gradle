import java.nio.file.Paths

plugins {
    id 'application'
    id 'ru.clevertec.checksystem.plugin.pdfprint' version "1.1.6"
}

group = 'ru.clevertec.checksystem'
version = '1.0'
description = 'checksystem-cli'

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_14
}

application {
    mainClass = "ru.clevertec.checksystem.cli.Main"
}

repositories {
    mavenCentral()
}

def coreProject = project(":checksystem-core");

dependencies {
    implementation coreProject
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.0"
}

sourceSets.main.resources.srcDirs += "${coreProject.projectDir}/src/main/resources"

// https://www.eclipse.org/aspectj/doc/released/README-194.html
def additionalJvmArgs = [
        "--add-opens",
        "java.base/java.lang=ALL-UNNAMED",
]

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true;
    doFirst {
        jvmArgs additionalJvmArgs
        coreProject.configurations.runtimeAgent.each { jvmArgs "-javaagent:${it.absolutePath}" }
    }
}

tasks.withType(JavaExec) {
    javaExec -> javaExec.doFirst {
        jvmArgs additionalJvmArgs
        coreProject.configurations.runtimeAgent.each { jvmArgs "-javaagent:${it.absolutePath}" }
    }
}

def template = true;

pdfprint {
    runTaskName = "run"

    inputFilePath = Paths.get("$rootDir", "..", "examples", "checks_serialized.json")
    inputFileFormat = "json"
    outputPdfPath = Paths.get("$rootDir", "resources", "CHECKS_" + (template ? "WITH" : "WITHOUT") + "_TEMPLATE.pdf")

    templateIsUsed = template;
    templateUrl = "https://github.com/stebadmitriy/files/raw/main/Clevertec_Template.pdf"
    topOffset = 94;
    templateOutput = Paths.get("$rootDir", "resources", "templates", "Clevertec_Template.pdf")
}
