plugins {
    id 'application'
}

group = 'ru.clevertec.checksystem'
version = '1.0'
description = 'checksystem-cli'

def coreProjectName = 'checksystem-core'

// https://www.eclipse.org/aspectj/doc/released/README-194.html
def additionalJvmArgs = ["--add-opens", "java.base/java.lang=ALL-UNNAMED"];

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_14
}

application {
    mainClass = "ru.clevertec.checksystem.cli.Main"
}

configurations {
    runtimeAgent
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":$coreProjectName")
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.0"
    runtimeAgent "org.aspectj:aspectjweaver:1.6.9"
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    "../$coreProjectName/src/main/resources/font",
                    "../$coreProjectName/src/test/resources"
            ]
        }
    }
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true;
    doFirst {
        configurations.runtimeAgent.each { jarFile -> jvmArgs "-javaagent:$jarFile.absolutePath" }
        jvmArgs additionalJvmArgs
    }
}

run {
    doFirst {
        configurations.runtimeAgent.each { jarFile -> jvmArgs "-javaagent:$jarFile.absolutePath" }
        jvmArgs additionalJvmArgs
    }
}
