plugins {
    id 'application'
}

group = 'ru.clevertec.checksystem'
version = '1.0'
description = 'checksystem-cli'

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_14
}

application {
    mainClass = "ru.clevertec.checksystem.cli.Main"
}

repositories {
    mavenCentral()
}

def coreProject = project(":checksystem-core");

dependencies {
    implementation coreProject
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.0"
}

sourceSets.main.resources.srcDirs += "${coreProject.projectDir}/src/main/resources"

// https://www.eclipse.org/aspectj/doc/released/README-194.html
def additionalJvmArgs = [
        "--add-opens",
        "java.base/java.lang=ALL-UNNAMED",
]

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true;
    doFirst {
        jvmArgs additionalJvmArgs
        jvmArgs "-javaagent:${coreProject.configurations.ajw.singleFile.absolutePath}"
    }
}

tasks.withType(JavaExec) {
    javaExec -> javaExec.doFirst {
        jvmArgs additionalJvmArgs
        jvmArgs "-javaagent:${coreProject.configurations.ajw.singleFile.absolutePath}"
    }
}
